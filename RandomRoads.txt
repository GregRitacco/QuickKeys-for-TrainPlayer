** RandomRoads script
** Randomly assign road names and numbers to freight cars
** and home road names and numbers to home road eqpt
** 2022 Greg Ritacco

Let version = 20220801

** Create pools of road names
  Let HOME_POOL = PRR
  Let HERITAGE_MANIFEST_POOL = PRR WJ&S PRR PRR PL PL PL NYNH&H P&R B&A B&O NYC M&P SP AT&SF DL&W CNJ ACL LV ER
  Let HERITAGE_REEFER_POOL = PFEX SFRD
  Let PRR_REEFER_POOL = FGEX
  Let HERITAGE_TANK_POOL = TCX PRR TELX
  Let HERITAGE_HEINZ_POOL = HKH
  Let HERITAGE_UNION_POOL = UTLX
  Let HERITAGE_HOPPER_POOL = PRR PGCC P&R B&O WCC
  Let MODERN_POOL = CSX NS BNSF UP CP NdeM UTLX ATTX

** Choose nothing, a space or a dash
  Let SEPERATOR = " "

** Set the length of the equipment number by type
  Let FREIGHT_LENGTH = 4
  Let HOME_LENGTH = 3
  Let PASS_LENGTH = 4
  Let MW_LENGTH = 3

  ** This is just here as a reference
  Let ALL_AAR_NA = E ES ED EE ET EP F FA FA6 FB FC FCA FD FL FM FS FW G GA GB GD GBH GE GS GT H HM HK HT LG LO LP LT O R RA RB RBL RP RS S SH SM T TA TG TL TM TW X XAR XMR XI XM XP M MWB MWC MWD MWG MWF MWK MWR MWX N NE NM B BE BH BM BMR BMT BP BR BX CA CS D DA DB DC DL DO DP DPA MA MB MR P PA PAS PB PBC PBO PC PL PO PS PSA
  Let ALL_AAR_UK = E ES ED EE ET EP F FA FA6 FB FC FCA FD FL FM FS FW G GA GB GD GBH GE GS GT H HM HK HT LG LO LP LT O R RA RB RBL RP RS S SH SM T TA TG TL TM TW X XAR XMR XI XM XP M MWB MWC MWD MWG MWF MWK MWR MWX N NE NM B BE BH BM BMR BMT BP BR BX CA CS D DA DB DC DL DO DP DPA MA MB MR P PA PAS PB PBC PBO PC PL PO PS PSA

** First cover all the basics
** Note that ET is taken out, steam tenders are not relabled
  Let HOME_AAR = E ES ESS ED EE EEI EP N NE NM
  Let MANIFEST_AAR = F FA FA6 FB FC FCA FD FL FM FS FW G GA GB GD GBH GE GS GT H HM LG LO LP LT O RA RBL RP S SH SM T TA TL TW X XAR XMR XI XM XP
  Let PASS_AAR = B BE BH BM BMR BMT BP BR BX CA CS D DA DB DC DL DO DP DPA MA MB MR P PA PAS PB PBC PBO PC PL PO PS PSA
  Let MW_AAR = M MWB MWC MWD MWG MWF MWK MWR MWX
** Then cover granular cases
  Let REEFER_AAR = RS R
  Let PRR_REEFER_POOL_AAR = R7 Rf RB RS
  Let HEINZ_REEFER_AAR = RH
  Let TANK_AAR = TG TM
  Let UNION_TANK_AAR = TO
  Let VINEGAR_AAR = TV
  Let HOPPER_AAR = HT HK

** Select which pools to use
  Let homeNames = @HOME_POOL
  Let passNames = @HOME_POOL
  Let mwNames = @HOME_POOL
  Let manifestNames = @HERITAGE_MANIFEST_POOL
  Let reeferNames = @HERITAGE_REEFER_POOL
  Let prrReeferNames = @PRR_REEFER_POOL
  Let heinzReeferNames = @HERITAGE_HEINZ_POOL
  Let tankNames = @HERITAGE_TANK_POOL
  Let unionTankNames = @HERITAGE_UNION_POOL
  Let vinegarNames = @HERITAGE_HEINZ_POOL
  Let hopperNames = @HERITAGE_HOPPER_POOL

*********************************************
Proc RandomNumber
** Generate a random road number

  Let result = ""

  Local length
  Let length = %1

  Local i
  Let i = 0
  While (@i < length)
    Let a = $Rand(0, 9)
    Let result = @a@@result
    Let i = @i + 1
  EndWhile

EndProc
*********************************************
Proc RelabelRollingStock
** Relabel the rolling stock with the randomly generated road and number

  Let randomRoadIndex = $Rand(0, @eqptNamesLen)
  Let randomRoad = $Set(@workingPool, Get, @randomRoadIndex)
  Call RandomNumber @roadNumberLength
  Let suffix = %1
  Let $Car(@rsLabel, Label) = @randomRoad@@suffix@@result

EndProc
*********************************************

Echo o2o Utility - Assign random road names and numbers to rolling stock
Echo Working on it......

** Run the script
Let allCarIds = $Layout(IDSet, CarIds)

Let rsLength = $Set(@allCarIds, Count)
Let i = 0
While (i < @rsLength)
  Let rsLabel = $Set(@allCarIds, Get, @i)
  Let rsAar = $Car(@rsLabel, AAR)

** First cover all the basics
** Relabel home equipment
Let workingAAR = @HOME_AAR
If ($Set(@workingAAR, Contains, @rsAar) = 1)
  Let roadNumberLength = @HOME_LENGTH
  Let workingPool = @homeNames
  Let eqptNamesLen = $Set(@homeNames, Count) - 1
  Call RelabelRollingStock @SEPERATOR
EndIf

** Relabel pool equipment
  Let workingAAR = @MANIFEST_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @manifestNames
    Let eqptNamesLen = $Set(@manifestNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel passenger equipment
Let workingAAR = @PASS_AAR
If ($Set(@workingAAR, Contains, @rsAar) = 1)
  Let roadNumberLength = @PASS_LENGTH
  Let workingPool = @passNames
  Let eqptNamesLen = $Set(@passNames, Count) - 1
  Call RelabelRollingStock @SEPERATOR
EndIf

** Relabel MOW equipment
Let workingAAR = @MW_AAR
If ($Set(@workingAAR, Contains, @rsAar) = 1)
  Let roadNumberLength = @MW_LENGTH
  Let workingPool = @mwNames
  Let eqptNamesLen = $Set(@mwNames, Count) - 1
  Call RelabelRollingStock MW
EndIf

** Then cover granular cases

** Relabel reefer equipment
  Let workingAAR = @REEFER_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @reeferNames
    Let eqptNamesLen = $Set(@reeferNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel tank equipment
  Let workingAAR = @TANK_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @tankNames
    Let eqptNamesLen = $Set(@tankNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel union tank equipment
  Let workingAAR = @UNION_TANK_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @unionTankNames
    Let eqptNamesLen = $Set(@unionTankNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel PRR reefer equipment
  Let workingAAR = @PRR_REEFER_POOL_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @prrReeferNames
    Let eqptNamesLen = $Set(@prrReeferNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel heinz reefer equipment
  Let workingAAR = @HEINZ_REEFER_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @heinzReeferNames
    Let eqptNamesLen = $Set(@heinzReeferNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel vinegar equipment
  Let workingAAR = @VINEGAR_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @vinegarNames
    Let eqptNamesLen = $Set(@vinegarNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

** Relabel hopper equipment
  Let workingAAR = @HOPPER_AAR
  If ($Set(@workingAAR, Contains, @rsAar) = 1)
    Let roadNumberLength = @FREIGHT_LENGTH
    Let workingPool = @hopperNames
    Let eqptNamesLen = $Set(@hopperNames, Count) - 1
    Call RelabelRollingStock @SEPERATOR
  EndIf

  Let i = @i + 1

EndWhile

Echo Relabeled equipment count: @rsLength
Echo RandomRoads @version
