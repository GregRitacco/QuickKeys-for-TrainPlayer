** o2o - ops 2 ops Workevent Switching - Version 2
Let version = 20220105

Let occupiedCarCodes = E,ES,ED,EE,ET,EP,NWC,MWX,B,BE,BH,CA,CS,N,NE,NM,D,DA,DB,DC,DL,DO,DP,DPA,MA,MB,MR,P,PA,PAS,PB,PBC,PBO,PC,PL,PO,PS,PSA


Proc Unschedule

  Echo Reset previous WorkEvent cars

  While($Set(%1, Count) <> 0)
    Let jCar = $String(%1, NextToken)
    Let jTemp = $Car(@jCar, Track)
    Let jTemp = $Track(@jTemp, Label)
    If(@jTemp = "")
      Let jTemp = "~"
    EndIf
    Let $Car(@jCar, Dest) = @jTemp
    Let $Car(@jCar, ExcludeOps) = 0
  EndWhile
  View Refresh

EndProc
****************************************************************************


Proc CreateReportHeader

  Echo Find the number of WorkEvents for the manifest
  ** And extract the manifest header

  Let jManifestCopy = %1
  Let j = 1
  Let jEventSet = ""

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** RR name
  If($String(@jTemp, StartsWith, "HN") = 1)
    Call $String(@jTemp, NextToken)
  Else
    Let jTemp = "Railroad name not found"
  Endif
  Let jHeader = @jTemp

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** Train name
  If($String(@jTemp, StartsWith, "HT") = 1)
    Call $String(@jTemp, NextToken)
  Else
    Let jTemp = "Train name not found"
  Endif
  Let jHeader = @jHeader + \n@jTemp

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** Train description
  If($String(@jTemp, StartsWith, "HD") = 1)
    Call $String(@jTemp, NextToken)
  Else
    Let jTemp = "Train description not found"
  Endif
  Let jHeader = @jHeader + \n@jTemp

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** Train comment
  If($String(@jTemp, StartsWith, "HC") = 1)
    Call $String(@jTemp, NextToken)
  Else
    Let jTemp = "Train comment not found"
  Endif
  Let jHeader = @jHeader + \n@jTemp

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** Valid time
  If($String(@jTemp, StartsWith, "HV") = 1)
    Call $String(@jTemp, NextToken)
  Else
    Let jTemp = "Train time stamp not found"
  Endif
  Let jHeader = @jHeader + \n@jTemp

  Let jTemp = $String(@jManifestCopy, NextToken, @xCR)  ** Work event total
  If($String(@jTemp, StartsWith, "WT") = 1)
    Call $String(@jTemp, NextToken)
    Let jWETotal = @jTemp
  Else
    Echo = "Total number of work events unknown"
    Let jWETotal = 0
  Endif

  Echo There are @jWETotal WorkEvents

EndProc
****************************************************************************


Proc ParseManifest ** Parse the whole manifest from JMRI

  Let y = 0 ** pickup count
  Let z = 0 ** setout count
  Let jMText = "" ** Reset jMessage text
  Let jManifestCopy = %1

  While($Set(@jmanifestCopy, Count) <> 0)
    Let jLine = $String(@jManifestCopy, NextToken, @xCR)

    If($Set(@jLine, Contains, "EOF") = 1)
      Echo Break
    EndIf

    Let j1 = $String(@jLine, NextToken) ** action to be taken WE PU SO PL SL

    If(@j1 = "WE")
      Let jWE = $String(@jLine, NextToken)
      Let jWL = $String(@jLine, NextToken)
      Echo WorkEvents @jWE @jWL
      Continue
    EndIf

    If(@jWE = %2)
      Let jWEN = @jWL
      Call ParseLine @jLine

      If(@j1 = "PC")
        Call PickUpCar
        Call LoadCar
        Call TagCar
        Let y = @y + 1
      EndIf
      If(@j1 = "SC")
        Call TagCar
        Call SetOutCar
        ** Call LoadCar
        Let z = @z + 1
      EndIf
      If(@j1 = "PL")
        Call TagCar
        Call PickUpLoco
      EndIf
      If(@j1 = "SL")
        Call TagCar
        Call SetOutLoco
      EndIf
    EndIf

  EndWhile
  Echo Current WorkEvent processed

EndProc
****************************************************************************


Proc ParseLine ** (All Cars) parse out all variables for each car in the manifest and format for switch list

  ** jNumbers are for switchlist

  Let j0 = " " ** create spaces in switch list

  Let j2 = $String(@jLine, NextToken) ** ID
  Let jLabel = $Car(@j2, Label)
  Let jID = @j2
  Let j2 = @j2 + "       "
  Let j2 = $Substr(0, 7, @j2) ** truncate to 8 char

  Let j3 = $String(@jLine, NextToken) ** RR
  Let j3 = @j3 + "          "
  Let j3 = $Substr(0, 5, @j3) ** truncate to 6 char

  Let j4 = $String(@jLine, NextToken) ** Number
  Let j4 = @j4 + "          "
  Let j4 = $Substr(0, 5, @j4) ** truncate to 6 char

  Let j9 = $String(@jLine, NextToken) ** Car or Engine type

  Let lt = $String(@jLine, NextToken) ** Load Type
  Let j5 = "E"
  If @lt = "Load"
    Let j5 = "L"
  EndIf

  If(@x6 = 0) ** Using AO loads
    If($Set(@occupiedCarCodes, Contains, @j9) = 1)
      Let j5 = "O"
      Echo Yipee
    EndIf
  EndIf

  Let j6c = $String(@jLine, NextToken) ** Load
  Let j6c = @j6c + "                    "
  Let j6c = $Substr(0, 11, @j6c) ** truncate to 12 char
  let j6l = "           " ** Blank 12 char string

  Let j7 = $String(@jLine, NextToken) ** From locale
  Let jFromLoc = @j7
  Let j7 = @j7 + "                             "
  Let j7 = $Substr(0, 19, @j7) ** truncate to 20 char

  Let j8 = $String(@jLine, NextToken) ** To locale
  Let jToLoc = @j8
  Let j8 = @j8 + "                             "
  Let j8 = $Substr(0, 19, @j8) ** truncate to 20 char

EndProc
****************************************************************************


Proc TagCar

  Let jLocaleIndex = $Set(@jLocaleSet, Find, @jToLoc)
  Let jTag = $Set(@jTrackLabelSet, Get, @jLocaleIndex)

  Let $Car(@jLabel, Dest) = @jTag
  Let jFromList = $Set(@jFromList, Add, "@jID")
  ** Let jFromList = @jLabel + "," + @jFromList

EndProc
****************************************************************************


Proc PickupCar ** Tag the TP car to be picked up

  ** Note adding a whitespace between variables adds a whitespace to the report, @j0 is not needed

  Let jMText = @jMText + \n$Set(@o2oSet, Get, 29) @j3 @j4 @j5 @j6c $Set(@o2oSet, Get, 14) @j7
  Let $Car(@jLabel, ExcludeOps) = 0 ** set the tags color

EndProc
****************************************************************************


Proc SetoutCar ** Tag the TP car to be set out

  Let jMText = @jMText + \n$Set(@o2oSet, Get, 30) @j3 @j4 @j5 @j6c $Set(@o2oSet, Get, 15) @j8
  Let $Car(@jLabel, ExcludeOps) = 1

EndProc
****************************************************************************


Proc PickUpLoco ** Tag the engine to be picked up

  Let jMText = @jMText + \n$Set(@o2oSet, Get, 29) @j3 @j4 @j5 @j6l $Set(@o2oSet, Get, 14) @j7
  Let $Car(@jLabel, ExcludeOps) = 0 ** set the tags color

EndProc
****************************************************************************


Proc SetOutLoco ** Tag the engine to be set out

  Let jMText = @jMText + \n$Set(@o2oSet, Get, 30) @j3 @j4 @j5 @j6l $Set(@o2oSet, Get, 15) @j8
  Let $Car(@jLabel, ExcludeOps) = 1 ** set the tags color

EndProc
****************************************************************************


Proc LoadCar

  ** if using JMRI custom loads
  If(@x6 = 1)
    Let $Car(@jLabel, Loaded) = 1
    Let $Car(@jLabel, LoadName) = @jLoad
  EndIf

  ** If using AO loads
  If(@x6 = 0)
    Let $Car(@jLabel, Loaded) = 0

    ** First, try to load from current location
    Let jIndexLocs = $Ops(Find, Industries.Locale, @jFromLoc) ** set of indeces
    While($Set(@jIndexLocs, Count) <> 0)
      Let jIndex = $String(@jIndexLocs, NextToken)
      Let jAAR = $Ops(Get, Industries.AAR, @jIndex)
      If($Car(@jLabel, AAR) = @jAAR)
        Let jSR = $Ops(Get, Industries.S/R, @jIndex)
        If(@jSR = "S")
        If(@j5 = "L")
          Let $Car(@jLabel, LoadName) = $Ops(Get, Industries.Load, @jIndex)
          Let $Car(@jLabel, Loaded) = 1
          ** Echo Load car @jLabel with $Ops(Get, Industries.Load, @jIndex)
        EndIf
        EndIf
      EndIf
    EndWhile

    ** Second, try to load from next location
    Let jIndexLocs = $Ops(Find, Industries.Locale, @jToLoc) ** set of indeces
    While($Set(@jIndexLocs, Count) <> 0)
      Let jIndex = $String(@jIndexLocs, NextToken)
      Let jAAR = $Ops(Get, Industries.AAR, @jIndex)
      If($Car(@jLabel, AAR) = @jAAR)
        Let jSR = $Ops(Get, Industries.S/R, @jIndex)
        If(@jSR = "R")
          Let $Car(@jLabel, LoadName) = $Ops(Get, Industries.Load, @jIndex)
          Let $Car(@jLabel, Loaded) = 1
          ** Echo Load car @jLabel with $Ops(Get, Industries.Load, @jIndex)
        Else
          ** Let $Car(@jLabel, Loaded) = 0
        EndIf
      EndIf
    EndWhile

    ** Third, try to load from the cars FD
    Let jIndexLocs = $Ops(Find, Industries.Locale, @jFDLoc) ** set of indeces
    While($Set(@jIndexLocs, Count) <> 0)
      Let jIndex = $String(@jIndexLocs, NextToken)
      Let jAAR = $Ops(Get, Industries.AAR, @jIndex)
      If($Car(@jLabel, AAR) = @jAAR)
        Let jSR = $Ops(Get, Industries.S/R, @jIndex)
        If(@jSR = "R")
          Let $Car(@jLabel, LoadName) = $Ops(Get, Industries.Load, @jIndex)
          Let $Car(@jLabel, Loaded) = 1
          ** Echo Load car @jLabel with $Ops(Get, Industries.Load, @jIndex)
        Else
          ** Let $Car(@jLabel, Loaded) = 0
        EndIf
      EndIf
    EndWhile
  EndIf

EndProc
****************************************************************************


Proc ReadAoLocations

  Echo Read the AO location dataset for this layout

  Let jTemp = $Ops(Find, Locations.Locale, All) ** returns indices
  Let jLocaleSet = $Ops(Get, Locations.Locale, @jTemp) ** set of all locales
  Let jTrackLabelSet = $Ops(Find, Locations.Track, All, Track) ** set of all track labels

EndProc
****************************************************************************


Proc IncrementWE

  Call Unschedule  @jFromList
  Let jFromList = ""

  Call ParseManifest @jManifestAll @jIndexWE
  Let jNote = @jHeader \n$Set(@o2oSet, Get, 4) @jWEN $Set(@o2oSet, Get, 12) @jIndexWE $Set(@o2oSet, Get, 13) @jWETotal \n@jMText \n\n@y $Set(@o2oSet, Get, 5) @z $Set(@o2oSet, Get, 6) \n
  Call NoteWindow @jNote 1
  Let L = 1

EndProc
****************************************************************************


If (%1 = SetUp)

  Let x5 = 1
  Let x6 = %2

  Let xMatrix = @x0,@x1,@x2,@x3,@x4,@x5,@x6,@x7
  ** Call $System(MD "%homepath%\JMRI\TrainPlayer")
  Call o2oInit
  Let jFromList = $Car($Car, label)
  Let jManifestAll = ""
  Let jLocList = "Location List,"
  Call $View(Hide, "Track Labels")
  Call $View(Show,"car ids")
  View Refresh
  If (@x6 = 0)
    Echo Using AO Industry loads
  Else
    Echo Using JMRI custom loads
  Endif

  Echo o2o @version

EndIf
****************************************************************************


If($Set(@xMatrix, Get, 0) = 1) ** quick keys turned on
If($Set(@xMatrix, Get, 5) = 1)

  If ($Key = 76) ** key l for list
    If(@jManifestAll = "") ** if manifest is not read in, set to default.
      Let jNote = \n$Set(@o2oSet, Get, 0)\n\n$Set(@o2oSet, Get, 1)\n\n$Set(@o2oSet, Get, 2) \n
    EndIf
    If (@L = 0)
      Let L = 1
      Call NoteWindow @jNote 1
    Else
      Let L = 0
      Call NoteWindow
    EndIf
    Let H = 0
    Let U = 0
    Let P = 0
  EndIf
  ****************************************************************************


  If($Key = 77) ** key m for manifest
    Let jMessage = $Set(@o2oSet, Get, 19) ** Read in jManifest
    Let jManifest = $Msgbox(YN, @jMessage)
    If (@jManifest = 0)
      Let $Key = 0
      Exit
    Endif

    ** Create a location matrix from the AO location dataset
    Call ReadAoLocations

    Let jReportManifest = $DataDir + "\Reports\JMRI Report - Work Events.csv"
    Let jManifest = $File(@jReportManifest, Open)
    Let jManifestAll = $File(@jManifest, ReadAll)
    Call $File(@jReportManifest, Close)
    Call CreateReportHeader @jManifestAll

    Let jNote = \n$Set(@o2oSet, Get, 0)\n\n$Set(@o2oSet, Get, 3) \n
    Call NoteWindow @jNote 1
    Echo Read in manifest
    Let jIndexWE = 1
    Call IncrementWE
    View Refresh
  EndIf
  ****************************************************************************


  If($Key = 33) ** Page Up key
    If(@jIndexWE = 1)
      Let jMessage = $Set(@o2oSet, Get, 17)
      Call $Msgbox(@jMessage)
    Else
      Let jNote = @jHeader \n\n$Set(@o2oSet, Get, 9)\n\n$Set(@o2oSet, Get, 11) \n
      Call NoteWindow @jNote 1
      Echo Move to previous WorkEvent
      Let jIndexWE = @jIndexWE - 1

      Call IncrementWE
      View Refresh
    EndIf
  EndIf
  ****************************************************************************


  If($Key = 34) ** Page Down key
    If(@jIndexWE = @jWETotal)
      Let jMessage = $Set(@o2oSet, Get, 18)
      Call $Msgbox(@jMessage)
    Else
      Let jNote = @jHeader \n\n$Set(@o2oSet, Get, 8)\n\n$Set(@o2oSet, Get, 11) \n
      Call NoteWindow @jNote 1
      Echo Move to next WorkEvent
      Let jIndexWE = @jIndexWE + 1

      Call IncrementWE
      View Refresh
    EndIf
  EndIf
  ****************************************************************************


  If ($Key = 35) ** End key
    If(@jIndexWE = @jWETotal)
      Let jMessage = $Set(@o2oSet, Get, 18)
      Call $Msgbox(@jMessage)
    Else
      Let jNote = @jHeader \n\n$Set(@o2oSet, Get, 10)\n\n$Set(@o2oSet, Get, 11) \n
      Call NoteWindow @jNote 1
      Echo Move to last WorkEvent
      Let jIndexWE = @jWETotal

      Call IncrementWE
      View Refresh
    EndIf
  EndIf
  ****************************************************************************


  If ($Key = 36) ** Home key
    If(@jIndexWE = 1)
      Let jMessage = $Set(@o2oSet, Get, 17)
      Call $Msgbox(@jMessage)
    Else
      Let jNote = @jHeader \n\n$Set(@o2oSet, Get, 7)\n\n$Set(@o2oSet, Get, 11) \n
      Call NoteWindow @jNote 1
      Echo Move to first WorkEvent
      Let jIndexWE = 1

      Call IncrementWE
      View Refresh
    EndIf
  EndIf
  ****************************************************************************


EndIf
EndIf
