** TurnTable Version 2


** How to:
** Add a command box
** Edit in script editor
** press record
** manually move the turntable around
** copy from command box into Turntable Matrices note window
** edit to add turntable lead number or letter, IE
** Edit:
** rotate 1 386
** Into:
** rotate d 1 386

** after the Turntable matrices note is finished,
** press apply
** save the layout
** restart the master script

If (%1 = SetUp)

  Let x4 = 1
  Let TTableSpeedDivisor = 5
  Let tScriptID = %2
  Call SetUpTurntable
  Let T = 1 ** turntable sub on
  Echo TurnTable Version 2
EndIf
****************************************************************************

Proc CullTheHerd ** make a sub matrix of tracks connected to the selected turntable

  Let tMatrixCopy = ""
  Let i = 0
  While(@i < $Set(@tmatrix, Count))
    Let tTemp = $Set(@tMatrix, Get, @i)
    If(@tTemp = @tTable)
      Let tMatrixCopy = @tMatrixCopy + $Set(@tMatrix, Get, @i) +, ** TurnTable ID
      Let i = @i + 1
      Let tMatrixCopy = @tMatrixCopy + $Set(@tMatrix, Get, @i) +, ** Lead Track
      Let i = @i + 1
      Let tMatrixCopy = @tMatrixCopy + $Set(@tMatrix, Get, @i) +, ** Junction ID
    EndIf
    Let i = @i + 1
  EndWhile

EndProc
****************************************************************************

Proc SetUpTurntable

Let tMatrix = ""
** Make a dictionary
Let tAlpha = 0,1,2,3,4,5,6,7,8,9,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,
Let tNumeric = 48,49,50,51,52,53,54,55,56,57,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80,81,82,83,84,85,86,87,88,89,90,
Let tNoteText = $Note(@tScriptID)
While(@tNoteText <> "")
  Let tLine = $String(@tNoteText, NextToken, @xCR)
  If($String(@tLine, Contains, "rotate") = 1)
    Call $String(@tLine, NextToken, " ") ** Strip off the word 'rotate'

    Let tL = $String(@tLine, NextToken, " ") ** the alpha character of the track Lead in tNoteText
    Let tL = $Set(@tAlpha, Find, @tL) ** the index of the alpha character
    Let tL = $Set(@tNumeric, Get, @tL) ** the key number of the alpha character

    Let tT = "T" + $String(@tLine, NextToken, " ") ** add a 'T' to the Table ID number

    Let tJ = "J" + @tLine ** add a 'J' to the junction number
  
    Let tMatrix = $Set(@tMatrix, Add, @tT)
    Let tMatrix = $Set(@tMatrix, Add, @tL)
    Let tMatrix = $Set(@tMatrix, Add, @tJ) ** write the matrix: Table,Lead,Junction

  EndIf
EndWhile
*** Echo @tMatrix
EndProc
****************************************************************************


If($Key > 47) ** a number is pressed
If($Key < 58)
If(@T = 1) ** turntable sub on
If(@x0 = 1) ** all QK active
  Let H = 1
  Let U = 1
  Let T = 0
  Let x0 = 0 ** shut off QK
  Let tTable = $Key ** key number of TT lead
  If ($Set(@tMatrix, Contains, @tTable) = 1) ** is a valid lead selected
    Let tTable = $Set(@tMatrix, Find, @tTable) ** index of TT lead
    Let tTable = @tTable - 1 ** index of TT
    Let tTable = $Set(@tMatrix, Get, @tTable) ** TT ID in matrix

    Call CullTheHerd

    Let xy = $Set(@tNumeric, Find, $Key)
    Let xz = $Set(@tAlpha, Get, @xy)
    Echo
    Echo Turntable lead @xz is selected.
    Echo Select an alignment track.

    On Key
      Let x0 = 0 ** shut off QK
      Let tTrack = $Key
      If($Set(@tMatrixCopy, Contains, @tTrack) = 1)
        Let tTrack = $Set(@tMatrixCopy, Find, @tTrack) ** track index
        Let tTrack = @tTrack + 1 ** junction index
        Let tTrack = $Set(@tMatrixCopy, Get, @tTrack) ** junction id

        Let xy = $Set(@tNumeric, Find, $Key)
        Let xz = $Set(@tAlpha, Get, @xy)

        Echo Align to track @xz
        Echo Press 'a' for anticlockwise or 'c' for clockwise.
        TryAgain:
        On Key
          Let x0 = 0 ** shut off QK
          If ($Key = 65)
            Rotate @tTable @tTrack CCW
            Echo Running...
          ElseIf ($Key = 67)
            Rotate @tTable @tTrack CW
            Echo Running...
          Else
            Echo Press 'a' for anticlockwise or 'c' for clockwise.
            GoTo TryAgain
          EndIf
          On TableStop
            Echo Turntable aligned to track @xz
            Let x0 = 1
            Let H = 0
            Let U = 0
            Let T = 1
            View Refresh
            Exit
      Else
        Echo Stall track not found.
        Echo Select a turntable lead.
        Let x0 = 1
        Let H = 0
        Let U = 0
        Let T = 1
        Exit
      EndIf
  Else
    Echo Not a valid table lead.
    Let x0 = 1
    Let H = 0
    Let U = 0
    Let T = 1
    Exit
  EndIf

EndIf
EndIf
EndIf
EndIf
****************************************************************************
