** Quick Keys Entities


Proc ao_reInit
** Reworks the Advanced Ops setup

  Call $View(Hide,"Track Labels")
  Call $View(hide,tracks)
  Call $View(hide,rails)
  Call $View(hide,ties)
  Call $View(hide,roadbed)
  tools run

EndProc
****************************************************************************

Proc ApplyVersionItems
** Apply version specific stuff

  Let Version = $Setting(InstalledVer)

  If ($String(@Version, StartsWith, 7) = 1)
    Let ViewList = @ViewList7
  EndIf
  If ($String(@Version, StartsWith, 8) = 1)
    Let ViewList = @ViewList8
  EndIf

EndProc
****************************************************************************

Proc SetLanguage

** Read in text for Quick Keys help page
  Let HelpLoc = $DataDir + "\Subroutines\QuickKeys\qkBundle\" + %1 + "\Help.txt"
  Let HelpFile = $File(@HelpLoc, Open)
  Let helpBundle = $file(@HelpFile, ReadAll)
  Call $File(@HelpLoc, Close)

** Read in text for Quick Keys pop up messages
  Let MessageLoc = $DataDir + "\Subroutines\QuickKeys\qkBundle\" + %1 + "\Message.txt"
  Let MessageFile = $File(@MessageLoc, Open)
  Let messageBundle = $file(@MessageFile, ReadAll)
  Call $File(@MessageLoc, Close)

** Read in text for Quick Keys utility menu
  Let UtilityLoc = $DataDir + "\Subroutines\QuickKeys\qkBundle\" + %1 + "\Utility.txt"
  Let UtilityFile = $File(@UtilityLoc, Open)
  Let utilityBundle = $file(@UtilityFile, ReadAll)
  Call $File(@UtilityLoc, Close)

** Read in text for o2o reports
  Let o2oLoc = $DataDir + "\Subroutines\QuickKeys\qkBundle\" + %1 + "\o2o.txt"
  Let o2oFile = $File(@o2oLoc, Open)
  Let o2oBundle = $File(@o2oFile, ReadAll)
  Call $File(@o2oLoc, Close)

** Read in text for the welcome message
  Let welcLoc = $DataDir + "\Subroutines\QuickKeys\qkBundle\" + %1 + "\Welcome.txt"
  Let welcFile = $File(@welcLoc, Open)
  Let welcBundle = $File(@welcFile, ReadAll)
  Call $File(@welcLoc, Close)

EndProc
***************************************************************************

Proc makeHelpPage

  Let helpPage = ""

  If (@x0 = 0) ** All of Quick Keys is off

    Let helpPage = @helpPage \n$Set(@helpBundle, Get, 0)

  EndIf

  If (@x0 = 1) ** All of Quick Keys is on

    Let helpPage = @helpPage \n$Set(@helpBundle, Get, 1)
    If (@x1 = 1)
      If (@x2 = 0)
        Let helpPage = @helpPage $Set(@helpBundle, Get, 2)
      EndIf
      If (@x2 = 1)
        Let helpPage = @helpPage $Set(@helpBundle, Get, 3)
      EndIf
    EndIf
    If (@x3 = 1)
      Let helpPage = @helpPage $Set(@helpBundle, Get, 4)
    EndIf
    If (@x4 = 1)
      Let helpPage = @helpPage $Set(@helpBundle, Get, 5)
    EndIf
    If (@x5 = 1)
      Let helpPage = @helpPage $Set(@helpBundle, Get, 6)
    EndIf
    If (@x6 = 1)
      Let helpPage = @helpPage $Set(@helpBundle, Get, 7)
    EndIf

  EndIf

EndProc
***************************************************************************

Proc makeUtilityMenu

  Let utilityMenu = ""

  If (@x0 = 0) ** All of Quick Keys is off

    Let utilityMenu = \n$Set(@utilityBundle, Get, 0)

  EndIf

  If (@x0 = 1) ** All of Quick Keys is on

    Let utilityMenu = $Set(@utilityBundle, Get, 1) ** Header

    If (@x1 = 1) ** Speed Switching is activated
      If (@x2 = 0) ** Phake Physics is off
        Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 2)
      EndIf
      If (@x2 = 1) ** Phake Physics is on
        Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 3)
      EndIf
    EndIf

    Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 4) ** Add Personalized Settings
    Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 5) ** Add RandomRoads template
    Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 6) ** Apply RandomRoads

    If (@x5 = 1)
      Let utilityMenu = @utilityMenu + $Set(@utilityBundle, Get, 7)
    EndIf

  EndIf

  Let utilityMenu = @utilityMenu + \n\n$Set(@utilityBundle, Get, 9) \n ** Footer

EndProc
***************************************************************************

Proc MakePersonalizedSettings

  Let SettingsNote = $Note(@NoteForPersonalizedSettings)
  Let settingsFile = $DATADIR + "\Subroutines\QuickKeys\PersonalizedSettings.txt"
  Call $Write(@settingsFile, @SettingsNote)

EndProc
***************************************************************************

Proc MakeProcXYZ
** Make the code snippet in the selected notes into a procedure

  Let xyzPrefix = pressX,pressY,pressZ
  Let xyzNoteNumber = @NoteForPressX,@NoteForPressY,@NoteForPressZ
  Let i = 0
  While (@i < 3)

    Let procFileName = $DATADIR + "\Subroutines\QuickKeys\" + $Set(@xyzPrefix, get, @i) + ".txt"
    Let procNoteNumber = $Set(@xyzNoteNumber, get, @i)
    Let procNoteContents = $Note(@procNoteNumber)
    If (@procNoteNumber = 0)
      Let procNoteContents = "Echo No script is mapped to this key press"
    EndIf

    Call $Write(@procFileName, @procNoteContents)

    Let i = @i + 1

  EndWhile

EndProc
***************************************************************************

Proc CopyPersonalizedSettingsTemplate

** Input a note number
  Input noteId $Set(@messageBundle, Get, 20)

** Read in RandomRoads template
  Let rrTemplate = $DataDir + "\Subroutines\QuickKeys\qkSupport\PersonalizedSettingsTemplate.txt"
  Let rrText = $Read(@rrTemplate)

** Copy to note
  Let $Note(@noteId) = @rrText

EndProc
***************************************************************************

Proc CopyRandomRoadsTemplate

** Input a note number
  Input noteId $Set(@messageBundle, Get, 20)

** Read in RandomRoads template
  Let rrTemplate = $DataDir + "\Subroutines\QuickKeys\qkSupport\RandomRoadsTemplate.txt"
  Let rrFile = $File(@rrTemplate, Open)
  Let rrText = $file(@rrFile, ReadAll)
  Call $File(@rrFile, Close)
** Copy to note
  Let $Note(@noteId) = @rrText

EndProc
***************************************************************************

Proc OpenHelpNote

  If (@H = 0)
    Let H = 1
    Let U = 0
    Let L = 0
    Call makeHelpPage
    Call NoteWindow @helpPage 1
  Else
    Let H = 0
    Call NoteWindow
  EndIf
  Let $Key = 0
  Let U = 0

EndProc
****************************************************************************

Proc OpenUtilityNote

  If (@U = 0) ** Create message for utility menu
    Call makeUtilityMenu
    Call NoteWindow @utilityMenu 1
    Let U = 1
  Else
    Call NoteWindow
    Let U = 0
  EndIf
  Let $Key = 0
  Let H = 0
  Let L = 0

EndProc
****************************************************************************

Proc DisplayFullScreen

  If (@F = 0)
    Let F = 1
    Call $View(show,"Full Screen")
  Else
    Let F = 0
    Call $View(hide,"Full Screen")
  EndIf
  NoteBox "." ** Necessray to select the current layout so QK continues to work
  NoteBox

EndProc
****************************************************************************

Proc ToggleQuickKeys

  If (@x0 = 0)
    Let x0 = 1 ** activate all Quick keys
  Else
    Let x0 = 0 ** deactivate all Quick keys
  EndIf
  Let $Key = 0
  Let U = 0
  Let H = 0
  Let L = 0

EndProc
****************************************************************************

Proc ToggleToolBars

  Let i = 0
  While (@i < $Set(@ViewList, Count))

    Let temp = $Set(@ViewList, get, @i)
    Call $View(Toggle, @temp)

    Let i = @i + 1

  EndWhile

EndProc
***************************************************************************

Proc ProcessorAdjustment

Echo Phake Physics adjustment for processor speed

* Call $System("%homepath%\AppData\Roaming\TrainPlayer\Subroutines\QuickKeys\qkSupport\ProcessorInfo.bat") ** This is the WMIC command

Let xCSLoc = $DataDir + "\Reports\ProcessorInfo.txt"
If($File(@xCSLoc, Exists) = 1)
  Let xCSFile = $File(@xCSLoc, Open)
  While(1 = 1)
    Let xLine = $File(@xCSFile, ReadLine)
    If ($String(@xLine, StartsWith, "MaxClockSpeed") = 1)
      Call $String(@xLine, NextToken, "=")
      Let xPS = @xLine ** xPS is the processor speed
      Echo Processor speed: @xps
    EndIf
    If ($String(@xLine, StartsWith, "DeviceID") = 1)
      Call $String(@xLine, NextToken, "=")
      Let xCPU = @xCPU + 1 ** xCPU is the number of processors
      Echo Number of processors: @xCPU
    EndIf
    If (@xLine = "EOF")
      Call $File(@xCSFile, Close)
      Break
    EndIf
  EndWhile
Else
  Let xPS = 2500
  Let xCPU = 1
  Echo ProcessorInfo.txt is missing
EndIf
Let xCurve = 0,1.0,1.5,1.75,2.0,2.25,2.5,2.75,3.0, ** Power of computer as processors are added
Let xCS = @xPS * $Set(@xCurve, Get, @xCPU)

EndProc
****************************************************************************
